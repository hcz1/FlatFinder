buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'

jar {
    baseName = 'C02015'
    version = '0.0.1-SNAPSHOT'
}

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    // Tomcat
    compile("org.apache.tomcat.embed:tomcat-embed-jasper:8.0.23")
    compile("javax.servlet:jstl:1.2")
    // JPA
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    runtime("mysql:mysql-connector-java")
    // Security
    compile("org.springframework.boot:spring-boot-starter-security")
    testCompile("org.springframework.security:spring-security-test:4.0.1.RELEASE")
    // Security tag library
    compile('org.springframework.security:spring-security-taglibs:4.0.4.RELEASE')
    //Java Mail
    compile('com.sun.mail:javax.mail:1.5.5')
    //Google Geocoding API
    compile ('com.google.maps:google-maps-services:0.1.12')
    // Cucumber
    testCompile("info.cukes:cucumber-spring:1.2.3")
    testCompile('info.cukes:cucumber-java:1.2.3')
    testCompile('info.cukes:cucumber-junit:1.2.3')
    testCompile('junit:junit:4.11')
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'com.uni.c02015', 'src/test/resources']
        }
    }
}

checkstyle {
    toolVersion = "6.1.1"
    configFile = "./config/checkstyle/google_checks.xml" as File
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}